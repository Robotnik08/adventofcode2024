const input = read("input.txt")
const grid = stringSplit(input, "\n")

const n = (int)grid

const typed_set = {}
if grid#i#j != '.' then {
    set typed_set->(grid#i#j) = [] when typed_set->(grid#i#j) == null
    do push(typed_set->(grid#i#j), [j, i])
} for j in range(n) for i in range(n)

const anti_total, anti_total_2 = {}, {}

do {
    make i = 1
    set anti_total_2->(p#0) = true
    do {
        make anti_location = [p#0#0 + (p#0#0 - p#1#0) * i, p#0#1 + (p#0#1 - p#1#1) * i]
        if anti_location#0 >= 0 && anti_location#0 < n && anti_location#1 >= 0 && anti_location#1 < n then {
            if i == 1 then {
                set anti_total->(anti_location) = true
            }
            set anti_total_2->(anti_location) = true
            set i++
        } else {
            break
        }
    } while true
} for p in permutations(v, 2) for v in values(typed_set)

do sayln(`Part 1: {len(keys(anti_total))}`)
do sayln(`Part 2: {len(keys(anti_total_2))}`)