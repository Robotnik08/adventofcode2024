include "../visualization/graphics.to"

const input = read("input.txt")
const instructions = stringSplit(input, "\n")

const width, height = 101, 103

class Robot (robot_string) {
    const parts = stringSplit(robot_string, " ")
    const position_str, velocity_str = substring(parts#0, 2, parts#0 - 1), substring(parts#1, 2, parts#1 - 1)
    const p, v = stringSplit(position_str, ","), stringSplit(velocity_str, ",")
    set self->x, self->y, self->vx, self->vy = stringToInt(p#0), stringToInt(p#1), stringToInt(v#0), stringToInt(v#1)
    implement move() {
        set self->x += self->vx
        set self->y += self->vy

        set self->x += width when self->x < 0
        set self->y += height when self->y < 0

        set self->x %= width
        set self->y %= height
    }
}

const robots = []

do push(robots, Robot(i)) for i in instructions

// remove the out folder
do deleteDirectory("out", true) when directoryExists("out")
do createDirectory("out")

make window = createWindow("", width, height, WindowFlags->WINDOW_HIDDEN)

do r->move() for r in robots then generateImage(i) then sayln(i) for i in range(10000)

do closeWindow(window)

define generateImage (i) {
    do setColor(window, 0, 0, 0, 255)
    do clearWindow(window)

    do setColor(window, 255, 255, 255, 255)
    do drawPoint(window, r->x, r->y) for r in robots
    do toImage(window, `out/{i}.png`)
}